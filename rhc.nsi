; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "OpenShift-RHC"
!define PRODUCT_VERSION "1.9.3-1"
!define PRODUCT_PUBLISHER "OpenShift Origin"
!define PRODUCT_WEB_SITE "http://openshift.com/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${PRODUCT_NAME}"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define GIT_INSTALLER "Git-1.8.1.2-preview20130201.exe"

!define RUBY_VERSION "ruby-1.9.3-p484-i386-mingw32"
!define RUBY_INSTALLER "${RUBY_VERSION}.7z"

; MUI 1.67 compatible ------
!include "MUI.nsh"
!include LogicLib.nsh
!include nsDialogs.nsh

!addincludedir .\include
!include EnvVarUpdate.nsh

; 7zip support
!addplugindir ".\plugin"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "openshift.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "LICENSE"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY

Page custom proxyPage proxyPageLeave
Page custom libraServerPage libraServerPageLeave

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "rhc.bat"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; include for some of the windows messages defines
!include "winmessages.nsh"
; HKLM (all users) vs HKCU (current user) defines
!define env_hklm 'HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"'
!define env_hkcu 'HKCU "Environment"'

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Install.exe"
XPStyle on

InstallDir "$PROGRAMFILES\openshift-rhc"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show


Var HTTP_PROXY
Var proxyDialog
Var proxyLabel
Var proxyTextBox
Var proxyCheckbox

; dialog create function
Function proxyPage
  
  ; === libra_server (type: Dialog) ===
  nsDialogs::Create 1018
  Pop $proxyDialog
  ${If} $proxyDialog == error
    Abort
  ${EndIf}
  !insertmacro MUI_HEADER_TEXT "Configure HTTP Proxy (for gem install)" ""
  
  ; === Label ===
  ${NSD_CreateLabel} 8u 5u 91u 9u "HTTP Proxy"
  Pop $proxyLabel
  
  ; === TextBox ===
  ${NSD_CreateText} 8u 16u 124u 11u ""
  Pop $proxyTextBox
  
  ; === Checkbox ===
  ${NSD_CreateCheckbox} 8u 45u 250u 11u "&is OpenShift Server behind the proxy?"
  Pop $proxyCheckbox
  nsDialogs::Show $proxyDialog
FunctionEnd

; dialog leave function
Function proxyPageLeave
  ${NSD_GetText} $proxyTextBox $HTTP_PROXY 
  
  ${NSD_GetState} $proxyCheckbox $0
  ${If} $0 <> 0
	WriteRegExpandStr ${env_hklm} "LIBRA_SERVER_PROXY" "$HTTP_PROXY"
  ${Else}
	WriteRegExpandStr ${env_hklm} "LIBRA_SERVER_PROXY" ""
  ${EndIf}
FunctionEnd


Var libraServerDialog
Var libraServerLabel
Var libraServerTextBox

; dialog create function
Function libraServerPage
  
  ; === libra_server (type: Dialog) ===
  nsDialogs::Create 1018
  Pop $libraServerDialog
  ${If} $libraServerDialog == error
    Abort
  ${EndIf}
  !insertmacro MUI_HEADER_TEXT "Configure OpenShift RHC" ""
  
  ; === Label ===
  ${NSD_CreateLabel} 8u 5u 91u 9u "OpenShift API URL"
  Pop $libraServerLabel
  
  ; === TextBox ===
  ${NSD_CreateText} 8u 16u 124u 11u ""
  Pop $libraServerTextBox
  
  
  nsDialogs::Show $libraServerDialog
FunctionEnd

; dialog leave function
Function libraServerPageLeave
  ${NSD_GetText} $libraServerTextBox $0
  ; set variable
  WriteRegExpandStr ${env_hklm} "LIBRA_SERVER" "$0"
FunctionEnd

Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

Section "copy files" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "rhc"
  File "rhc.bat"
  File "openshift.ico"
  CreateDirectory "$SMPROGRAMS\OpenShift"
  CreateShortCut "$SMPROGRAMS\OpenShift\rhc.lnk" "$INSTDIR\rhc.bat"
  CreateShortCut "$DESKTOP\rhc.lnk" "$INSTDIR\rhc.bat"
SectionEnd

Section "install git bash" SEC02
  SetOutPath "$INSTDIR"
  ; download and install
  NSISdl::download /PROXY "$HTTP_PROXY" "https://msysgit.googlecode.com/files/${GIT_INSTALLER}" "${GIT_INSTALLER}"
  Pop $R0
  ${If} $R0 == 'success'
    ; download successful
    StrCpy $0 ''
    ; run the one click installer
    ExecWait '"${GIT_INSTALLER}" /nocancel' $0
    ${If} $0 == ''
	  ; execution of one click installer failed
	  MessageBox MB_OK "git install failed."
    ${EndIf}
	Delete "$INSTDIR\${GIT_INSTALLER}"
  ${Else}
    ; download not successfull
    MessageBox MB_OK "git download failed."
  ${EndIf}
SectionEnd

Section "install ruby" SEC03
  SetOutPath "$INSTDIR"
  ; download and install
  NSISdl::download /PROXY "$HTTP_PROXY" "http://dl.bintray.com/oneclick/rubyinstaller/${RUBY_INSTALLER}?direct" "${RUBY_INSTALLER}"
  Pop $R0
  ${If} $R0 == 'success'
    ; download successful
    StrCpy $0 ''
    SetOutPath $INSTDIR
    SetCompress off
    DetailPrint "Extracting package..."
    SetDetailsPrint listonly
    SetCompress auto
    SetDetailsPrint both
    ; run the one click installer
    Nsis7z::ExtractWithDetails "${RUBY_INSTALLER}" "Installing ruby %s..."
    Rename "$INSTDIR\${RUBY_VERSION}\" "$INSTDIR\ruby\"

	Delete "$OUTDIR\${RUBY_INSTALLER}"
  ${Else}
    ; download not successfull
    MessageBox MB_OK "ruby download failed."
  ${EndIf}
SectionEnd

Section "install rhc" SEC04
  SetOutPath "$INSTDIR"
   
  StrCpy $1 ''
  ; install rhc locally
  ${If} $HTTP_PROXY == ''
    ExecWait '"$INSTDIR\ruby\bin\gem.bat" install rhc --no-rdoc --no-ri' $1
  ${Else}
    ExecWait '"$INSTDIR\ruby\bin\gem.bat" install --http-proxy "$HTTP_PROXY" rhc --no-rdoc --no-ri' $1
  ${EndIf}
  ${If} $1 == ''
    MessageBox MB_OK "Gem install failed. Please install the rhc gem manually"
  ${EndIf}
SectionEnd

Section -AdditionalIcons
  CreateShortCut "$SMPROGRAMS\rhc\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\rubyinstaller.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\openshift.ico"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  ${EnvVarUpdate} $0 "PATH" "A" "HKCU" "$INSTDIR"  
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "You have successfully uninstalled $(^Name)."
FunctionEnd

Function un.onInit
!insertmacro MUI_UNGETLANGUAGE
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\rhc.bat"
  Delete "$INSTDIR\rhc"
  RMDir /r "$INSTDIR\ruby"

  Delete "$SMPROGRAMS\rhc\Uninstall.lnk"
  Delete "$SMPROGRAMS\rhc\Website.lnk"
  Delete "$DESKTOP\rhc.lnk"
  Delete "$SMPROGRAMS\rhc\rhc.lnk"
  Delete "$INSTDIR\openshift.ico"

  RMDir "$SMPROGRAMS\rhc"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
  ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "$INSTDIR"  
SectionEnd
